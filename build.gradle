plugins {
    id 'java'
    id 'xyz.jpenilla.run-paper' version '3.0.2'
    id 'xyz.jpenilla.resource-factory-paper-convention' version '1.3.1'
    id 'com.gradleup.shadow' version '9.2.2'
    id 'io.freefair.lombok' version '9.0.0'
}

group = 'it.dominick'
version = '1.0.0'

ext {
    MC_VERSION = '1.21.1'
    PAPER_VERSION = "${MC_VERSION}-R0.1-SNAPSHOT"
    JAVA_VERSION = 21
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(JAVA_VERSION)
    }
}

repositories {
    mavenCentral()
    gradlePluginPortal()

    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${PAPER_VERSION}"

    compileOnly 'org.jetbrains:annotations:24.1.0'

    implementation 'net.kyori:adventure-text-minimessage:4.17.0'
    implementation 'net.kyori:adventure-text-serializer-legacy:4.17.0'
    implementation 'com.zaxxer:HikariCP:7.0.2'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release = JAVA_VERSION
}


shadowJar {
    archiveClassifier.set('')

    dependencies {
        exclude(dependency('org.projectlombok:lombok'))
    }
}

runServer {
    minecraftVersion(MC_VERSION)
}

paperPluginYaml {
    main = 'it.dominick.th.TreasureHunt'
    apiVersion = '1.21'
    authors.add('Dominick12')
}